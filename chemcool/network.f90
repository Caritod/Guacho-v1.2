!=======================================================================
!> @file network.f90
!> @brief chemical network module
!> @author A. Castellanos, A. Rodriguez, A. Raga and A. Esquivel
!> @date 24/Nov/2014

! Copyright (c) 2014 A. Esquivel et al.
!
! This file is part of Guacho-3D.
!
! Guacho-3D is free software; you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation; either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
! GNU General Public License for more details.
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see http://www.gnu.org/licenses/.
!=======================================================================

!> @brief Chemical/atomic network module
!> @details this module should be generated by an interface code.

 module network

  implicit none

  ! number of species
  integer, parameter :: n_spec = 4

  ! number of equilibrium species
  integer, parameter:: nequil = 2

  ! number of total elements
  integer, parameter :: n_elem = 1

  ! number of non-equilibrium equations
  integer, parameter :: n_nequ = n_spec - nequil 

  ! indexes of the different species
  integer, parameter :: iHII = 1
  integer, parameter :: iH2  = 2
  integer, parameter :: iHI = 3
  integer, parameter :: ie= 4
  !   Integer, parameter :: iM  = n_spec+1

  ! indexes of the equilibrium species
  integer, parameter :: iHt = 1

  ! number of species with 
  integer, parameter :: iHn = 3

  ! number of reaction rates
  integer, parameter :: n_reac = 7

  ! indexes of the different rates
  integer, parameter :: iR1 = 1
  integer, parameter :: iR2 = 2
  integer, parameter :: iR3 = 3
  integer, parameter :: iR4 = 4
  integer, parameter :: iR5 = 5
  integer, parameter :: iR6 = 6
  integer, parameter :: iR7 = 7

  ! code variables
  real (kind=8) :: y(n_spec), dydt(n_spec), y0(n_elem)
  real (kind=8) :: rate(n_reac),jac(n_spec,n_spec)

  contains

!======================================================================  

  subroutine derv(y,rate,dydt,y0)

    implicit None
    real (kind=8), intent(in)  ::   y0(n_elem)
    real (kind=8), intent(in)  ::    y(n_spec)
    real (kind=8), intent(out) :: dydt(n_spec)
    real (kind=8), intent(in)  :: rate(n_reac)

    dydt(iHII)=   rate(iR4)*y(iHI)*y(ie ) - rate(iR5)*y(iHII)*y(ie)
    dydt(iH2 )= - rate(iR1)*y(iH2)*y(iH2) - rate(iR2)*y(iH2 )*y(iHI)   &
                - rate(iR3)*y(iH2)*y(ie ) + rate(iR6)*y(iHI )*y(iHI)  &
                + rate(iR7)*y(iHI)*y0(iHt)

    !  equilibrium reactions
    dydt(iHI)  = -2.*y(iH2) - y(iHI) - y(iHII) + y0(iHt)
    dydt(ie  ) = y(ie)-y(iHII)

  end subroutine derv

  !======================================================================

  subroutine get_jacobian(y,jacobian,rate)
  
  implicit None
  real (kind=8), intent(in)  :: y(n_spec)
  real (kind=8), intent(out) :: jacobian(n_spec,n_spec)
  real (kind=8), intent(in)  :: rate(n_reac) 

  !  HII
  jacobian(iHII,iHII)=-rate(iR5)*y(ie)        
  jacobian(iHII,iH2 )= 0.d0
  jacobian(iHII,iHI )= rate(iR4)*y(ie)
  jacobian(iHII,ie  )= rate(iR4)*y(iHI)-rate(iR5)*y(iHII)

  !  H2
  jacobian(iH2,iHII)= 0.d0     
  jacobian(iH2,iH2 )= -2.*rate(iR1)*y(iH2)  -   rate(iR2)*y(iHI) - rate(iR3)*y(ie)
  jacobian(iH2,iHI )=  -  rate(iR2)*y(iH2) + 2.*rate(iR6)*y(iHI) &
                  +  rate(iR7)*y(iHI)*(2*y(iH2)+y(iHI)+y(iHII))
  jacobian(iH2,ie  )=  -  rate(iR3)*y(iH2)

  !  HI
  jacobian(iHI,iHII)= 1.d0      
  jacobian(iHI,iH2 )= 2.d0
  jacobian(iHI,iHI )= 1.d0
  jacobian(iHI,ie  )= 0.d0

  !  e
  jacobian(ie,iHII)=  1.d0
  jacobian(ie,iH2 )=  0.d0
  jacobian(ie,iHI )=  0.d0
  jacobian(ie,ie  )= -1.d0

  end subroutine get_jacobian

  !======================================================================

  subroutine get_reaction_coefficients(rate,T)
  
  implicit None
  real (kind=8) :: T,T300
  real (kind=8),intent(out) ::rate(n_reac)
  
    T300=T/300.

    rate(iR1) = 1.d-8       * T300**0.d0     * exp(-8.41d4 /T)
    rate(iR2) = 4.67d-7     * T300**(-1.d0)  * exp(-5.5d4  /T)
    rate(iR3) = 3.22d-9     * T300**(0.35d0) * exp(-1.02d5 /T)
    rate(iR4) = 1.00978d-9  * T300**(0.5d0)  * exp(-1.578d5/T)
    rate(iR5) = 4.074746d-12* T300**(-0.79d0)* exp(-0.d0   /T)
    rate(iR6) = 8.14d-17    * T300**(-0.5d0) * exp(-4.48d0 /T)  
    rate(iR7) = 5.2e-17*sqrt(T300)

  end subroutine get_reaction_coefficients

end Module network

!======================================================================